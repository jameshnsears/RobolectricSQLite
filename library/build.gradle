apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    // drop "-all" from jar artifact
    archiveClassifier = null
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/jameshnsears/RobolectricSQLite")
            credentials {
                // from local.properties, then ~/.bashrc
                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                username = properties.getProperty("GITHUB_USERNAME") ?: System.getenv("GITHUB_USERNAME")
                password = properties.getProperty("GITHUB_TOKEN") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)

            groupId = 'com.github.guness'
            artifactId = 'robolectricsqlite'
            version = robolectricsqlLite_version
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.almworks.sqlite4java:sqlite4java:' + almworks_version

    implementation 'com.google.guava:guava:30.0-jre'

    implementation project(":binaries")
}

shadowJar {
    dependencies {
        exclude(dependency('com.almworks.sqlite4java:sqlite4java:' + almworks_version))
        exclude(dependency('com.google.guava:guava:30.0-jre'))

    }
}
